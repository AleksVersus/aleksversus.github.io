QSP-Game ! выводит нужный текст
$args[0] = $args[0]	&	!	какой текст выбрать
$args[1], $args[2], $args[3] и т.д.	&	!	модификации текста
# base.txt
$args[9] = $args[9]
if $args[0] = 'table.mainsets':
!@ рисуем табличку с начальными характеристиками
	!@ здесь $args[1] — выброшенное на кубиках значение
	if $AVS_PLAYERTYPE['size']="MINI":
	!@ для мобильной версии это не табличка, а своего рода список вариантов
		$args['res']=''
		$args['2.2']='&nbsp;'
		$args['2.3']='&nbsp;'
		$args['2.4']='&nbsp;'
		$args['2.5']='&nbsp;'
		$args['2.6']='&nbsp;'
		$args['2.7']='&nbsp;'
		$args['2.8']='&nbsp;'
		$args['2.9']='&nbsp;'
		$args['2.10']='&nbsp;'
		$args['2.11']='&nbsp;'
		$args['2.12']='&nbsp;'
		if $args[1]<>'':
			$args[$args[1]]='<span style="color:#aaaa00;">'	& !@ выбранный вариант подсвечивается жёлтым цветом
			$args['1:'+$args[1]]='</span>'
			$args['2.'+$args[1]]='&gt;'	& !@ выбранный вариант отмечается символом >
			$args['c']='Вариант|'
			$args['1.2']='[cur]&nbsp;2|2[/cur]'
			$args['1.3']='[cur]&nbsp;3|3[/cur]'
			$args['1.4']='[cur]&nbsp;4|4[/cur]'
			$args['1.5']='[cur]&nbsp;5|5[/cur]'
			$args['1.6']='[cur]&nbsp;6|6[/cur]'
			$args['1.7']='[cur]&nbsp;7|7[/cur]'
			$args['1.8']='[cur]&nbsp;8|8[/cur]'
			$args['1.9']='[cur]&nbsp;9|9[/cur]'
			$args['1.10']='[cur]10|10[/cur]'
			$args['1.11']='[cur]11|11[/cur]'
			$args['1.12']='[cur]12|12[/cur]'
		else
			$args['c']='На кубиках|'
			$args['1.2']='&nbsp;2'
			$args['1.3']='&nbsp;3'
			$args['1.4']='&nbsp;4'
			$args['1.5']='&nbsp;5'
			$args['1.6']='&nbsp;6'
			$args['1.7']='&nbsp;7'
			$args['1.8']='&nbsp;8'
			$args['1.9']='&nbsp;9'
			$args['1.10']='10'
			$args['1.11']='11'
			$args['1.12']='12'
		end
		$args['res']+=$args['c']+'ЛОВКОСТЬ|СИЛА|ОБАЯНИЕ<br>'
		$args['res']+='<h3>'+$args['2']+$args['2.2']+$args['1.2']+'|&nbsp;8|22|8&nbsp; &nbsp;'+$args['1:2']+$args['8']+$args['2.8']+$args['1.8']+'10|16|7'+$args['1:8']+'</h3>'
		$args['res']+='<h3>'+$args['3']+$args['2.3']+$args['1.3']+'|10|20|6&nbsp; &nbsp;'+$args['1:3']+$args['9']+$args['2.9']+$args['1.9']+'&nbsp;8|24|7'+$args['1:9']+'</h3>'
		$args['res']+='<h3>'+$args['4']+$args['2.4']+$args['1.4']+'|12|16|5&nbsp; &nbsp;'+$args['1:4']+$args['10']+$args['2.10']+$args['1.10']+'&nbsp;9|22|6'+$args['1:10']+'</h3>'
		$args['res']+='<h3>'+$args['5']+$args['2.5']+$args['1.5']+'|&nbsp;9|18|8&nbsp; &nbsp;'+$args['1:5']+$args['11']+$args['2.11']+$args['1.11']+'10|18|7'+$args['1:11']+'</h3>'
		$args['res']+='<h3>'+$args['6']+$args['2.6']+$args['1.6']+'|11|20|6&nbsp; &nbsp;'+$args['1:6']+$args['12']+$args['2.12']+$args['1.12']+'11|20|5'+$args['1:12']+'</h3>'
		$args['res']+='<h3>'+$args['7']+$args['2.7']+$args['1.7']+'|&nbsp;9|20|7&nbsp; &nbsp;'+$args['1:7']+'</h3>'
	else
		$args['res']='<table border=1 cellpadding=5 width=100%>'
		$args[$args[1]]=' bgcolor=#ffffbb'
		if $args[1]='':
			$args['c']='На кубиках:'
			$args['1.2']='2'
			$args['1.3']='3'
			$args['1.4']='4'
			$args['1.5']='5'
			$args['1.6']='6'
			$args['1.7']='7'
			$args['1.8']='8'
			$args['1.9']='9'
			$args['1.10']='10'
			$args['1.11']='11'
			$args['1.12']='12'
		else
			$args['c']='Варианты:'
			$args['1.2']='[cur]2|2[/cur]'
			$args['1.3']='[cur]3|3[/cur]'
			$args['1.4']='[cur]4|4[/cur]'
			$args['1.5']='[cur]5|5[/cur]'
			$args['1.6']='[cur]6|6[/cur]'
			$args['1.7']='[cur]7|7[/cur]'
			$args['1.8']='[cur]8|8[/cur]'
			$args['1.9']='[cur]9|9[/cur]'
			$args['1.10']='[cur]10|10[/cur]'
			$args['1.11']='[cur]11|11[/cur]'
			$args['1.12']='[cur]12|12[/cur]'
		end
		$args['res']+='<tr name=mainsets><td align=center width=25%>'+$args['c']+'</td><td align=center width=25%>ЛОВКОСТЬ</td><td align=center width=25%>СИЛА</td><td align=center width=25%>ОБАЯНИЕ</td></tr>'
		$args['res']+='<tr '+$args['2']+'><td align=center >'+$args['1.2']+'</td><td align=center>8</td><td align=center>22</td><td align=center>8</td></tr>'
		$args['res']+='<tr '+$args['3']+'><td align=center >'+$args['1.3']+'</td><td align=center>10</td><td align=center>20</td><td align=center>6</td></tr>'
		$args['res']+='<tr '+$args['4']+'><td align=center >'+$args['1.4']+'</td><td align=center>12</td><td align=center>16</td><td align=center>5</td></tr>'
		$args['res']+='<tr '+$args['5']+'><td align=center >'+$args['1.5']+'</td><td align=center>9</td><td align=center>18</td><td align=center>8</td></tr>'
		$args['res']+='<tr '+$args['6']+'><td align=center >'+$args['1.6']+'</td><td align=center>11</td><td align=center>20</td><td align=center>6</td></tr>'
		$args['res']+='<tr '+$args['7']+'><td align=center >'+$args['1.7']+'</td><td align=center>9</td><td align=center>20</td><td align=center>7</td></tr>'
		$args['res']+='<tr '+$args['8']+'><td align=center >'+$args['1.8']+'</td><td align=center>10</td><td align=center>16</td><td align=center>7</td></tr>'
		$args['res']+='<tr '+$args['9']+'><td align=center >'+$args['1.9']+'</td><td align=center>8</td><td align=center>24</td><td align=center>7</td></tr>'
		$args['res']+='<tr '+$args['10']+'><td align=center >'+$args['1.10']+'</td><td align=center>9</td><td align=center>22</td><td align=center>6</td></tr>'
		$args['res']+='<tr '+$args['11']+'><td align=center >'+$args['1.11']+'</td><td align=center>10</td><td align=center>18</td><td align=center>7</td></tr>'
		$args['res']+='<tr '+$args['12']+'><td align=center >'+$args['1.12']+'</td><td align=center>11</td><td align=center>20</td><td align=center>5</td></tr>'
		$args['res']+='</table>'
	end
	jump 'end_keep'
end
if $args[0]='table.numbers':
!@ отрисовка таблички с числами удачи и неудачи
	!@ циклом добавляем к списку чисел удачи числа, записанные в массиве $num_luck
	args['i']=0
	:numluck
	if args['i']<arrsize('$num_luck') and args['i']<6:
		$args['luck']+=$num_luck[args['i']]
		args['i']+=1
		if args['i']<arrsize('$num_luck'): $args['luck']+=',&nbsp;'
		jump 'numluck'
	end
	!@ так же циклом добавляем к списку чисел неудачи числа, записанные в массив $num_unluck
	args['i']=0
	:unluck
	if args['i']<arrsize('$num_unluck') and args['i']<6:
		$args['unluck']+=$num_unluck[args['i']]
		args['i']+=1
		if args['i']<arrsize('$num_unluck'): $args['unluck']+=',&nbsp;'
		jump 'unluck'
	end
	if $AVS_PLAYERTYPE['size']="MINI":
		!@ в любом плеере mini таблица растягивается на 100% экрана
			$args['res']='<table width=100% cellspacing=0>'
	elseif $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']="qSpider":
		!@ в навигаторе и qSpider поведение таблицы определяется стилями
		$args['res']='<table cellspacing=0 class="avs_game_stat_table">'
	else
		!@ в классическом плеере табличка всегда размером в 280 пикселей
		$args['res']='<table width=280 cellspacing=0>'
	end
	$args['res']+='<tr name=luck bgcolor=#e1ffe1><td align=left><span style="color:#000000;">Числа удачи:</span></td><td align=center><span style="color:#000000;font-weight:bolder;">'+$args['luck']+'</span></td></tr>'
	$args['res']+='<tr name=unluck bgcolor=#ffe1e1><td align=left><span style="color:#000000;">Числа неудачи:</span></td><td align=center><span style="color:#000000;text-decoration:line-through;">'+$args['unluck']+'</span></td></tr>'
	$args['res']+='</table>'
	jump 'end_keep'
end
if $args[0] = 'table.char':
!@ отрисовка таблички характеристик персонажа (героя)
	if $AVS_PLAYERTYPE['size']="MINI":
		!@ для мобильной версии табличка растягивается на ширину страницы
		$args['res']='<table width=100% cellspacing=0>'
	elseif $AVS_PLAYERTYPE['html_support']='qSpider':
		!@ для навигатора и паука поведение таблички определяется стилями CSS
		$args['res']='<table class="avs_game_stat_table">'
	else
		!@ для классического плеера ширина таблички равна 280 пикс.
		$args['res']='<table width=280 cellspacing=0>'
	end
	$args['res']+='<tr name=fst bgcolor=#e1ffff><td align=left><span style="color:#000000;">Ловкость:</span></td><td align=right><span style="color:#000000;"><<fast["now"]>>/<b><<fast["start"]>></b></span></td></tr>'
	$args['res']+='<tr name=pwr bgcolor=#ffffe1><td align=left><span style="color:#000000;">Сила:</span></td><td align=right><span style="color:#000000;"><<power["now"]>>/<b><<power["start"]>></b></span></td></tr>'
	$args['res']+='<tr name=scl bgcolor=#fff0ff><td align=left><span style="color:#000000;">Обаяние:</span></td><td align=right><span style="color:#000000;"><<social["now"]>>/<b><<social["start"]>></b></span></td></tr>'
	$args['res']+='</table>'
	jump 'end_keep'
end
if $args[0] = 'table.blast':
!@ вывод таблички предметов
	if $AVS_PLAYERTYPE['size']="MINI":
		!@ для мобильной версии таблица растягивается на весь экран
		$args['res']='<table width=100% cellspacing=0>'
	elseif $AVS_PLAYERTYPE['html_support']='qSpider':
		!@ для навигатора и паука таблица поведенствуется через CSS
		$args['res']='<table class="avs_game_objs_table">'
	else
		!@ для классического плеера ширина таблицы 280 пикселей
		$args['res']='<table width=280 cellspacing=0>'
	end
	killvar '$temp_id'	& !@ на всякий случай удаляем временный массив
	gosub 'get.daughter.obj','INVENTORY','','$temp_id'	& !@ получаем список предметов (вернее их идентификаторов)
	:blast
	if arrsize('$temp_id')>0:
		!@ строка объекта часть тела в таблице данных:
		args['pos.body']=arrpos('$id_array',$temp_id[0])
		!@ строка дочернего объекта в таблице данных
		args['pos.obj']=arrpos('$position_array',$temp_id[0])
		$args['hide']='false'	& !@ нужно ли скрыть предмет, на начальном этапе - нет
		if args['pos.obj']<>-1:
			!@ если дочерний объект есть, пытаемся вывести его
			if instr($object_array[args['pos.obj']],'[hide.inInv]')<>0:
				$args['hide']='true'	& !@ в свойствах предмета прописано скрытие в инвентаре, нет смысла считать всякие плюхи
			else
				!@ если предмет не скрыт, считаем плюхи
				if instr($object_array[args['pos.obj']],'[quest]')<>0:
				!@ подсветка квестового предмета
					$args['color']='114444'
					$args['attrib']='<b>'
					$args['/attrib']='</b>'
					$args['bgcolor']='fcfcfc'
				elseif instr($object_array[args['pos.body']],'[:место_в_рюкзаке:]')=0:
				!@ подсветка активного предмета
					$args['color']='008888'
					$args['attrib']='<b>'
					$args['/attrib']='</b>'
					$args['bgcolor']='fcfcfc'
				else
				!@ подсветка неактивного предмета
					$args['color']='008888'
					$args['attrib']=''
					$args['/attrib']=''
					$args['bgcolor']='fcfcfc'
				end
				!@ генерируем ссылку на обработчик предмета
				$args['name']='<a href="exec:gosub ''onObjClick'',''<<$id_array[args["pos.obj"]]>>''" class="plain">'
				$args['name']+='<font color=#'+$args['color']+'>'+$args['attrib']
				$args['name']+=$func('get.word.padez',$func('get.tag.cont',$object_array[args['pos.obj']],'name'),'И')
				$args['name']+=$args['/attrib']+'</font></a>'
				args['charge']=charge_array[args['pos.obj']]	& !@ текущий заряд
				args['maxcharge']=func('get.tag.num',$object_array[args['pos.obj']],'maxchrg')	& !@ получаем максимальный уровень заряда
				if instr($object_array[args['pos.obj']],'[maxchrg:')<>0 and args['maxcharge']=0:
					!@ если максимальный уровень не получен, но в признаках был определён
					$args['ch']='<span style="color:#000000;">'+$str(args['charge'])+'<span style="font-weight:bold;font-size:large">/∞</span></span>'
				elseif instr($object_array[args['pos.obj']],'[maxchrg:')<>0 and args['maxcharge']<>0:
					!@ если максимальный уровень получен
					$args['ch']='<span style="color:#000000;">'+$str(args['charge'])+'<span style="font-weight:bold;">/'+$str(args['maxcharge'])+'</span></span>'
				else
					$args['ch']='&nbsp;'
				end
			end
		else
			!@ если дочернего объекта нет, можно вывести часть теле или рюкзака
			if instr($object_array[args['pos.body']],'[hide.inInv]')<>0:
				!@ если часть теле скрыта в инвентаре, не высчитываем плюшки
				$args['hide']='true'
			else
				!@ высчитываем плюшки
				$args['color']='888888'
				$args['attrib']=''
				$args['/attrib']=''
				$args['name']='<font color=#'+$args['color']+'>'+$args['attrib']
				$args['name']+=$func('get.tag.cont',$object_array[args['pos.body']],'name')
				$args['name']+=$args['/attrib']+'</font>'
				$args['bgcolor']='fcfcfc'
				$args['ch']='&nbsp;'
			end
		end
		if $args['hide']='false':
			!@ если предмет не скрыт, добавляем его, как строку в таблицу
			$args['res']+='<tr name=blastobj bgcolor=#'+$args['bgcolor']+'><td align=left>'+$args['name']+'</td><td align=right>'+$args['ch']+'</td></tr>'
		end
		killvar 'temp_id',0	& !@ удаляем идентификатор предмета из списка
		jump 'blast'
	end
	$args['res']+='</table>'
end
:невозможные_действия
if $args[0]='f':
	if $args[1]='место_в_рюкзаке': $args['res']='Вы не можете взять это. Рюкзак полон.'
	if $args[1]='пояс': $args['res']='Вы не можете надеть на себя два пояса.'
	if $args[1]='лицо': $args['res']='Вы не можете надеть ещё одну маску на лицо.'
	if $args[1]='голова': $args['res']='Вы не можете надеть ещё один головной убор.'
	if $args[1]='пояс_фляга': $args['res']='У вас уже есть фляга.'
	if $args[1]='плечи': $args['res']='У вас на плечах уже что-то надето.'
	if $args[1]='шея': $args['res']='У вас на шее уже что-то надето.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='невозможное_действие':
	$args['res']="Пример невозможного действия."
	$args['add.colors and attributes']='nope'
end
if $args[0]='empt':
	$args['res']='Сосуд пуст.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='n':
	$args['res']='Нельзя выбросить этот предмет. Возможно он необходим для выполнения задания.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='notspec':
	$args['res']='Нет подходящих условий для использования предмета.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='notput':
	$args['res']='Здесь и сейчас вы не можете выбросить этот предмет.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='discharge':
	$args['res']='Этим предметом больше нельзя воспользоваться.'
	$args['add.colors and attributes']='nope'
end
if $args[0]='gift.no':
	$args['res']='К сожалению, в кошельке больше не осталось монет.'
	$args['add.colors and attributes']='nope'
end

:одевание
if $args[0]='used':
	if $args[1]='лицо': $args['res']='Вы надели '+$args[2]+'.'
	if $args[1]='пояс': $args['res']='Вы надели '+$args[2]+'.'
	if $args[1]='голова': $args['res']='Вы надели '+$args[2]+'.'
	$args['add.colors and attributes']='used'
end
if $args[0]='одевание':
	$args['res']="Пример действия надевания."
	$args['add.colors and attributes']='used'
end
:взять
if $args[0]='взять':
	$args['res']="Пример действия подъёма."
	$args['add.colors and attributes']='take'
end
if $args[0]='add':
	$args['name']=$func('get.word.padez',$func('get.tag.cont',$object_array[arrpos('$id_array',$args[1])],'name'),'В')
	$args['res']='Вы взяли '+$args['name']+'.'
	$args['add.colors and attributes']='take'
end
if $args[0]='get':
	$args['name']=$func('get.word.padez',$func('get.tag.cont',$object_array[arrpos('$id_array',$args[1])],'name'),'И')
	$args['res']='Получены '+$args['name']+'.'
	$args['add.colors and attributes']='take'
end

:выбросить
if $args[0]='выбросить':
	$args['res']="Пример действия выбрасывания."
	$args['add.colors and attributes']='put'
end
if $args[0]='put':
	$args['name']=$func('get.word.padez',$func('get.tag.cont',$object_array[arrpos('$id_array',$args[1])],'name'),'В')
	$args['res']='Вы выбросили '+$args['name']+'.'
	$args['add.colors and attributes']='put'
end
if $args[0]='gift':
	$args['name']=$func('get.word.padez',$func('get.tag.cont',$object_array[arrpos('$id_array',$args[1])],'name'),'В')
	$args['res']='Вы отдали '+$args['name']+'.'
	$args['add.colors and attributes']='put'
end
if $args[0]='s':
	$args['name']=$func('get.word.padez',$func('get.tag.cont',$object_array[arrpos('$id_array',$args[1])],'name'),'В')
	$args['res']='Вы продали '+$args['name']+'.'
	$args['add.colors and attributes']='put'
end

:лог боя
if $args[0]='war.round':
	!@ номера раундов
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<span class="avs_text_warround">Раунд <<GAME_WAR["round"]+1>></span>'
	else
		$args['res']="<font size=+1><b>Раунд <<GAME_WAR['round']+1>>.</b></font>"
	end
	jump 'end_keep'
end
if $args[0]='war':
	$args['href']='<a href="exec:gosub ''int.war''" title="вызов обработчика боя" class="plain">'
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<avs-enemy><br><br><br><center>'+$args['href']+'<span style="font-size:large">Сражаться</span></a></center></avs-enemy><br>'
	else
		$args['res']='<avs-enemy><br><br><br><center>'+$args['href']+'<font size=+1>Сражаться</font></a></center></avs-enemy><br>'
	end
end
if $args[0]='sell':
	$args['res']='<br><br><br><center>Чтобы продать предмет, нужно щёлкнуть по его названию и в появившемся меню выбрать пункт "Продать".</center>'
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_stat">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#008800><i>'+$args['res']+'</i></font>'
	end
end
if $args[0]='ed':
	args['rand']=rand(1,10)
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['open.f']='<span style="color:#998888;">'
		$args['close.f']='</span>'
	else
		$args['open.f']='<font color=#998888>'
		$args['close.f']='</font>'
	end
	$args['res']=$args['open.f']
	if $args[1]='female':
		if args['rand']=1: $args['res']+='Уснула навеки...'
		if args['rand']=2: $args['res']+='Покойся с миром...'
		if args['rand']=3: $args['res']+='Пала в смертельной схватке...'
		if args['rand']=4: $args['res']+='Не выдержала напряжения...'
		if args['rand']=5: $args['res']+='Погибла в этом бою...'
		if args['rand']=6: $args['res']+='Отправилась к праотцам...'
		if args['rand']=7: $args['res']+='Больше не поднимется...'
		if args['rand']=8: $args['res']+='Обрела вечный покой...'
		if args['rand']=9: $args['res']+='Теперь не угрожает вашей безопасности...'
		if args['rand']=10: $args['res']+='Ей уже ничего не нужно...'
	else
		if args['rand']=1: $args['res']+='Уснул навеки...'
		if args['rand']=2: $args['res']+='Покоится с миром...'
		if args['rand']=3: $args['res']+='Пал в смертельной схватке...'
		if args['rand']=4: $args['res']+='Не выдержал напряжения...'
		if args['rand']=5: $args['res']+='Погиб в этом бою...'
		if args['rand']=6: $args['res']+='Отправился к праотцам...'
		if args['rand']=7: $args['res']+='Больше не поднимется...'
		if args['rand']=8: $args['res']+='Обрёл вечный покой...'
		if args['rand']=9: $args['res']+='Теперь не угрожает вашей безопасности...'
		if args['rand']=10: $args['res']+='Ему уже ничего не нужно...'
	end
	$args['res']+=$args['close.f']
end
if $args[0]='attack':
	$args['res']='<a href="exec:gosub ''int.war.attack'',''<<$args[1]>>''"><font color=#886600>Сражаться</font></a>'
end
if $args[0]='eo':
	$args['res']='<font color=#888899>Вы сражаетесь с другим противником.</font>'
end
if $args[0]='eea':
	if $args[2]='female': $args['sex']='её' else $args['sex']='его'
	$args['res']='<font color=#888800>'+$args[1]+' атакует вас, но '+$args['sex']+' удар слишком слаб, чтобы причинить вам неудобства.</font>'
end
if $args[0]='wehn':
	$args['res']='<font color=#888800>Удар '+$args[1]+' был встречен вашим могучим ударом. Силы оказались равны.</font>'
end
if $args[0]='ef':
	$args['res']='<font color=#880000>'+$args[1]+' наносит вам удар. Ваша жизненная сила уменьшается.</font>
	<font size=-1 color=#880000>[<<power["now"]>> = <<power["now"]+args[2]>> - <<args[2]>>]</font>'
end
if $args[0]='hf':
	if $args[2]='female': $args['sex']='её' else $args['sex']='его'
	$args['res']='<font color=#008800>Вы наносите удар '+$args[1]+', и вам удаётся ранить '+$args['sex']+'.</font>
	<font size=-1 color=#008800>[<<args[3]>> = <<args[3]+2>> - 2]</font>'
end
if $args[0]='hve':
	$args['res']='<font color=#008888>Вы одолели '+$args[1]+'.</font>'
end
if $args[0]='hveb':
	$args['res']='<font color=#008888>Вы выхватываете бластер и одним выстрелом испепеляете '+$args[1]+'.</font>'
end
if $args[0]='blaster-attack':
	$args['res']='<a href="exec:gosub ''int.war.blaster'',''<<$args[1]>>''"><font color=#995500>Застрелить&nbsp;из&nbsp;бластера</font></a>'
end
if $args[0]='wv':
	if $args[2]!'': $args['res']='<center><font size=+1>'+$args[2]+'</font></center><br>'
	$args['res']+='<center><font size=+1><a href="exec:time[''goto'']=1 & goto ''<<$args[1]>>''" class="plain" title="wv">Вы победили!!!</a></font></center>'
end
if $args[0]='wp':
	if $args[2]!'': $args['res']='<center><font size=+1>'+$args[2]+'</font></center><br>'
	$args['res']+='<center><font size=+1><a href="exec:time[''goto'']=1 & goto ''<<$args[1]>>''" class="plain">Вы проиграли!!!</a></font></center>'
end
if $args[0]='wl':
	$args['res']='<br><br><center><font size=+1><a href="exec:goto ''[start]''" class="plain">Вы проиграли.<br>Начать с начала</a></font></center>'
end
:GAME_OVER
if $args[0]='go':
!@ ПОБЕДА В ИГРЕ !!!
	$args['res']='<br><br><center><a href="exec:goto ''[start]''" class="plain"><font size=+1>Победа!!! Игра пройдена.</font><br><font size=-1>(вернуться в главное меню)</font></a></center>'
end
if $args[0]='off':
	$args['res']='<br><br><center><font size=+1>Силы покинули вас.</font></center>'
end
if $args[0]='pow':
	if args[1]<0:	$args['res']='СИЛА уменьшается на <<-args[1]>>'
	if args[1]>0:	$args['res']='СИЛА увеличивается на <<args[1]>>'
	if args[1]=0:	$args['res']='СИЛА неизменна'
	$args['add.colors and attributes']='stat'
end
if $args[0]='lovk':
	if args[1]<0:	$args['res']='ЛОВКОСТЬ уменьшается на <<-args[1]>>'
	if args[1]>0:	$args['res']='ЛОВКОСТЬ увеличивается на <<args[1]>>'
	if args[1]=0:	$args['res']='ЛОВКОСТЬ неизменна'
	$args['add.colors and attributes']='stat'
end
if $args[0]='powup':
	if args[1]<0:	$args['res']='СИЛА уменьшается: <<args[1]>>'
	if args[1]>0:	$args['res']='СИЛА увеличивается: +<<args[1]>>'
	if args[1]=0:	$args['res']='СИЛА не изменилась.'
	$args['add.colors and attributes']='stup'
end
if $args[0]='lovkup':
	if args[1]<0:	$args['res']='ЛОВКОСТЬ уменьшается: <<args[1]>>'
	if args[1]>0:	$args['res']='ЛОВКОСТЬ увеличивается: +<<args[1]>>'
	if args[1]=0:	$args['res']='ЛОВКОСТЬ не изменилась.'
	$args['add.colors and attributes']='stup'
end
if $args[0]='изменение_стата':
	$args['res']='Изменение состояния.'
end
:обаяние
if $args[0]='soc':
	$args['res']='Требовалось обаяния <<args[1]>>. Ваше обаяние <<args[2]>>.'
end
if $args[0]='soc.prove':
	$args['res']='<a href="exec:gosub ''social'',''cube''" class="plain">Проверить обаяние</a>'
end
if $args[0]='soc.lose':
	$args['res']='<a href="exec:time[''goto'']=1 & social[''now'']-=1 & goto ''page.<<args[1]>>''" class="plain">Вам не удалось убедить собеседника в этот раз.</a>'
end
if $args[0]='soc.win':
	$args['res']='<a href="exec:time[''goto'']=1 & social[''now'']+=1 & goto ''page.<<args[1]>>''" class="plain">В этот раз вам удалось убедить собеседника.</a>'
end
:ловкость
if $args[0]='fst':
	$args['res']='Требовалось ловкости <<args[1]>>. Ваша ловкость <<args[2]>>.'
end
if $args[0]='fst' or $args[0]='soc':
	if args[1]>args[2]: $args['add.colors and attributes']='noit' else $args['add.colors and attributes']='ysit'
end
if $args[0]='fst.lose':
	$args['res']='<a href="exec:time[''goto'']=1 & goto ''page.<<args[1]>>''" class="plain">Вы оказались не слишком ловки в этот раз.</a>'
end
if $args[0]='fst.win':
	$args['res']='<a href="exec:time[''goto'']=1 & goto ''page.<<args[1]>>''" class="plain">В этот раз вы оказались достаточно ловки.</a>'
end
if $args[0]='fst.prove':
	$args['res']='<a href="exec:gosub ''fast'',''cube''" class="plain">Проверить ловкость</a>'
end
:выводим знания
if $args[0] = 'table.knew':
!@ табличка со знаниями
	$args['res']='<br><br>'
	if $AVS_PLAYERTYPE['size']="MINI":
		!@ для мобильной версии таблица растягивается на весь экран
		$args['res']+='<table width=100% cellspacing=0>'
	elseif $AVS_PLAYERTYPE['html_support']='qSpider':
		!@ для навигатора и паука таблица поведенствуется через CSS
		$args['res']+='<table class="avs_game_knews_table">'
	else
		!@ для классического плеера ширина таблицы 280 пикселей
		$args['res']+='<table cellspacing=0 width=100%>'
	end
	$args['res']+='<tr name=note bgcolor=#f9f9f9><td align=left>Ваши записи:</td></tr>'
	killvar '$temp_id'	& !@ на всякий случай удаляем временный массив
	gosub 'get.daughter.obj','KNOWLEDGE','','$temp_id'
	args['b']=arrsize('$temp_id')
	args['t']=0
	:knew
	if arrsize('$temp_id')>0:
		!@ строка объекта знание. Для чётных и нечётных строк отличается заливка бэкгрануда
		if args['i'] mod 2 =0: $args['color']='f9f9f9' else $args['color']='fdfdfd'
		args['pos.obj']=arrpos('$id_array',$temp_id[0])
		$args['name']=$func('get.tag.cont',$object_array[args['pos.obj']],'name')
		if instr($object_array[args['pos.obj']],'[выполнено]')<>0:
			!@ если задание выполнено, определяем одни настройки цвета
			$args['fc']='aaaaaa'
			$args['fin']=' (Сделано)'
		else
			$args['fc']='8888ff'
			$args['fin']=''
		end
		if instr($object_array[args['pos.obj']],'[hide]')=0 or $args[1]='test':
			$args['res']+='<tr bgcolor=#<<$args["color"]>> name=notetext><td align=left><span style="color:#<<$args["fc"]>>;"><i>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<<$args["name"]>><<$args["fin"]>></i></span></td></tr>'
			args['t']+=1
		end
		killvar 'temp_id',0
		args['i']+=1
		jump 'knew'
	end
	$args['res']+='</table>'
	if args['b']=0 or args['t']=0: $args['res']='&nbsp;'
	killvar '$temp_id'
	jump 'end_keep'
end
:game
if $args[0]='game.enemy':
	!@ выводим тексты по миниигре в кости
	$args['win']=$func('get.word.inPos',$args[1],1)
	$args['lose']=$func('get.word.inPos',$args[1],2)
	tvar['cube.hero']=tvar['cube.hero.1']+tvar['cube.hero.2']
	tvar['cube.enemy']=tvar['cube.enemy.1']+tvar['cube.enemy.2']
	$args['ссылка бросить']='<a href="exec:tvar[''cube.hero.1'']=rand(1,6) & tvar[''cube.hero.2'']=rand(1,6) & tvar[''cube.enemy.1'']=rand(1,6) & tvar[''cube.enemy.2'']=rand(1,6) & goto $curloc" class="plain">'
	$args['выпало']="Вам выпало <<tvar['cube.hero.1']>> и <<tvar['cube.hero.2']>>, вашему противнику <<tvar['cube.enemy.1']>> и <<tvar['cube.enemy.2']>>."
	if tvar['cube.hero']=0:
		$args['res']=$args['ссылка бросить']+'Бросить кубики</a>'
	elseif tvar['cube.hero']=tvar['cube.enemy']:
		$args['res']=$args['выпало']+'<br>Суммы выпавших очков равны.<br>'+$args['ссылка бросить']+'Бросить кубики ещё раз</a>'
	elseif tvar['cube.hero']>tvar['cube.enemy']:
		$args['res']=$args['выпало']+'<br>У вас выпало больше очков, чем у противника.<br>[page='+$args['win']+']Вы выиграли![/page]'
		$GAME_VALUE['game.cube.result']='win'
		GAME_VALUE['game.cube.party']+=1
	else
		$GAME_VALUE['game.cube.result']='lose'
		GAME_VALUE['game.cube.party']+=1
		$args['res']=$args['выпало']+'<br>У противника выпало больше очков, чем у вас.<br>[page='+$args['lose']+']Выиграл он![/page]'
	end
	$args['res']='<br><br><p align=center>'+$args['res']+'</p>'
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_stup">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#000044><i>'+$args['res']+'</i></font>'
	end
end
:сохранения
if $args[0]='сохранения':
	$args['res']='Пример текста при сохранении.' & $args['add.colors and attributes']='save'
end
if $args[0]='nosave':
	$args['res']='Сейчас нельзя сохранить игру.' & $args['add.colors and attributes']='save'
end
if $args[0]='save':
	$args['res']='Игра сохранена.' & $args['add.colors and attributes']='save'
end
if $args[0]='chpload':
	$args['res']='Контрольная точка загружена.' & $args['add.colors and attributes']='save'
end
if $args[0]='qckload':
	$args['res']='Быстрое сохранение загружено.' & $args['add.colors and attributes']='save'
end
if $args[0]='notchp':
	$args['res']='Контрольные точки ещё не созданы.' & $args['add.colors and attributes']='save'
end
if $args[0]='notqck':
	$args['res']='Быстрые сохранения не сделаны.' & $args['add.colors and attributes']='save'
end
:текст_по_модификатору_вероятностей
if $args[0]='vm':
	$args['res']="     Переход должен был выбросить вас где-то неподалёку от Ворот. Вот только <a href=""exec:gosub 'crt.obj','мв','','<knew>',0,1,'KNOWLEDGE' & time['goto']=1 & goto 'page.<<args[1]>>'"" class='plain'>где именно</a>? Эх, как бы пригодился модификатор вероятностей: нажал кнопку - и сразу к Воротам...
     Клуша мгновенно реагирует на этот мечтательный вздох: нет, модификатор не стоит использовать при Переходе, и - да, он у вас имеется.
     Вы тут же бросаете взгляд на браслет. Так и есть! Если смотреть под определённым углом, видны слабо светящиеся цифры красного и зелёного цветов. Господа учёные второпях забыли сообщить о столь полезном приборе.
     «Это новая модель, - гордо объявляет Клуша, - Модификатор реагирует на мыслеприказ, концентрирует до шести благоприятных исходов, анализирует удачу и...»
     «...отнимает шансы на успех в будущем!» - нетерпеливо заканчиваете вы.
     «...и позволяет восстанавливать исходные вероятностные потоки к благоприятному состоянию в случае наиболее успешного течения событий» - невозмутимо заканчивает КЛУ-6.
     «В случае успешного течения событий»... Как всегда, всё решает случай.
"
end
if $args[0]='vm_1':
	$args['res.1']="&nbsp;Эх, сейчас бы пригодился модификатор вероятностей!
     «Шеф, так ведь он у вас есть! -- моментально реагирует Клуша. -- Новая модель! C телепатическим управлением!»
     Отлично! Короткий мыслеприказ, и модификатор откликается. Мгновенный анализ возможных вероятностей и концентрация наиболее благоприятных исходов. Пусть это и отнимает шансы на успех в будущем, но сейчас вам позарез нужна удача!
"
	$args['res']="<p align=justify>[knew]мв|&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;Пришло время воспользоваться модификатором вероятностей. Короткий мыслеприказ, и модификатор откликается. Остаётся лишь положиться на удачу...|"+$args['res.1']+"[/knew]</p>

	"
end
if $args[0]='vm_2':
	$args['res']='<p align="center"><span style="color:#339900;">ПРОВЕРЬТЕ СВОЮ УДАЧУ</span>. Повезёт вам или нет?</</p>
	
<p align="center"><a href="exec:goto $curloc,$str(rand(1,6))" class="plain">Проверить удачу</a></p>'
end
if $args[0]='vm_3':
	$args['res']='<p align=center>На браслете замигало и погасло число <span style="color:#008800;"><<$args[1]>></span>. Через секунду оно уже светилось красным.

<a href="exec:gosub ''crt.obj'',''мв'','''',''<knew>'',0,1,''KNOWLEDGE'' & time[''goto'']=1 & goto ''page.<<args[2]>>''" class="plain">Вам повезло!</a></p>'
end
if $args[0]='vm_4':
	$args['res']='<p align=center>На браслете замигало и погасло число <span style="color:#880000;"><<$args[1]>></span>.

<a href="exec:gosub ''crt.obj'',''мв'','''',''<knew>'',0,1,''KNOWLEDGE'' & time[''goto'']=1 & goto ''page.<<args[2]>>''" class="plain">Вам не повезло...</a></p>'
end
if $args[0]='vm_5':
	$args['res']='<span style="color:#880000;">Произошла ошибка. Сделайте скриншот и обратитесь к разработчику</span>'
end
if $args[0]='note.objLoc':
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']="<span class=""avs_text_objonloc"">Предметы на локации:</span><br><br>"
	else
		$args['res']="<font size=-1>Предметы на локации:</font><br><br>"
	end
end
:отдать предмет
if $args[0]='help.putobj':
	$args['res']='Чтобы отдать предмет, просто щёлкните по его названию и выберите во всплывающем меню пункт "Выбросить". Ссылка перехода активируется после сброса необходимого количества предметов.'
	$args['add.colors and attributes']='stat'
end
!@ Общая схема стилизации текстовки
if $args['add.colors and attributes']='put':
	!@ аналог print.put...
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_put">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#886600>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='stat':
	!@ аналог print.stat...
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_stat">'+$args['res']+'</div>'
	else
		$args['res']='<span style="color:#008800;"><i>'+$args['res']+'</i></span>'
	end
end
if $args['add.colors and attributes']='save':
	!@ аналог print.save...
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<span class="avs_text_print_save">'+$args['res']+'</span>'
	else
		$args['res']='<span style="color:#990066;"><b>'+$args['res']+'</b></span>'
	end
end
if $args['add.colors and attributes']='stup':
	!@ аналог print.stup
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_stup">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#000044><i>'+$args['res']+'</i></font>'
	end
end
if $args['add.colors and attributes']='nope':
	!@ аналог print.nope
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_nope">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#880000>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='noit':
	!@ аналог print.noit
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_noit">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#662222>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='ysit':
	!@ аналог print.ysit
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_ysit">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#226622>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='prov':
	!@ аналог print.prov
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_prov">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#339900>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='take':
	!@ аналог print.take
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_take">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#008888>'+$args['res']+'</font>'
	end
end
if $args['add.colors and attributes']='used':
	!@ аналог print.used
	if $AVS_PLAYERTYPE['html_support']='qSpider':
		$args['res']='<div class="avs_text_print_used">'+$args['res']+'</div>'
	else
		$args['res']='<font color=#008800>'+$args['res']+'</font>'
	end
end
:зарядить_разрядить_бластер
if $args[0]='blaster.incharge' or $args[0]='blaster.decharge':
	if $AVS_PLAYERTYPE['size']="MINI":
		$args['c']='444499'
		$args['c2']='666699'
	else
		$args['c']='000044'
		$args['c2']='222244'
	end
	if $args[0]='blaster.incharge': $args['res']='<span style="color:#<<$args["c"]>>">Заряд бластера увеличивается: +<<args[1]>></span>'
	if $args[0]='blaster.decharge': $args['res']='<span style="color:#<<$args["c"]>>">Заряд бластера уменьшается: <<args[1]>></span>'
end
:жидкость
if $args[0]='liquid.drop':
	$args['res']='<span style="color:#884400;">Вы вылили <<$args[1]>> из фляги.</span>'
end
if $args[0]='liquid.full':
	$args['res']='<span style="color:#008888;">Вы наполнили флягу <<$args[1]>>.</span>'
end
if $args[0]='liquid.nopeflask':
	$args['res']='<span style="color:#880000;">У вас нет фляги.</span>'
end
:выбросить_рюкзак
if $args[0]='bag.drop':
	$args['res']='<span style="color:#886600;">Рюкзак выброшен. Вам больше некуда складывать вещи.</span>'
end
!@ выход и возвращение результата
:end_keep
$result=$args['res']
--- base.txt ---------------------------------
