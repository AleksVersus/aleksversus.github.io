# avs.save

! локация организует меню для сохраннения и загрузки состояний игры
! Меню генерится и обрабатывается только этой локацией. Реализовано за счёт того, что локации
! обрабатывающей пункт меню, передаётся аргументом номер выбранного пункта. Годится только для
! меню фиксированного размера.
$args['save.name']=$func('#zero#',4-len($str(save['count']+1)))+$str(save['count']+1)+'_'
$args['loc']=$strfind($curloc,'\d+')
$args['save.name']+=$func('#zero#',3-len($args['loc']))+$args['loc']
if args[0]=0:
	! генерим меню
	killvar '$temp_menu'
	$temp_menu[]="Сохранить:avs.save"
	$temp_menu[]="Загрузить:avs.save"
	$temp_menu[]='-:-'
	$temp_menu[]="Загрузить чекпоинт:avs.save"
	$temp_menu[]="-:-"
	$temp_menu[]="Быстрое сохранение:avs.save"
	$temp_menu[]="Быстрая загрузка:avs.save"
	! вызываем меню
	menu '$temp_menu'
	! уничтожаем меню
	killvar '$temp_menu'
elseif args[0]=1:
	if nosave=0:
		save['count']+=1
		savegame
		if $GAME_VALUE['save.reserve']<>'false': 
			if $AVS_PLAYERTYPE='': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
	else
		$print[]=$func('base.txt','nosave')
	end
elseif args[0]=2:
	opengame
elseif args[0]=4:
	if save['check']>0:
		! если плеер от сидхарты будет работать с сохранениями как навигатор if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['size']="MINI":
		if $AVS_PLAYERTYPE['type']="QN":
			opengame '1.sav'
		else
			opengame 'save/autosave.sav'
		end
	else
		$print[]=$func('base.txt','notchp')
	end
elseif args[0]=6:
	if nosave=0:
		save['qs']+=1
		save['count']+=1
		$save['type']='quick'
		!if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['size']="MINI":
		if $AVS_PLAYERTYPE['type']="QN":
			savegame '2.sav'
		else
			savegame 'save/quicksave.sav'
			if $GAME_VALUE['save.reserve']<>'false': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
		$print[]=$func('base.txt','save')
	else
		$print[]=$func('base.txt','nosave')
	end
elseif args[0]=7:
! быстрые сохранения
	if save['qs']>0:
		!if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['size']="MINI":
		if $AVS_PLAYERTYPE['type']="QN":
			opengame '2.sav'
		else
			opengame 'save/quicksave.sav'
		end
	else
		$print[]=$func('base.txt','notqck')
	end
elseif args[0]=999:
	if nosave=0:
		save['check']+=1
		save['count']+=1
		$save['type']='auto'
		!if $AVS_PLAYERTYPE['type']='QN' or $AVS_PLAYERTYPE['size']="MINI":
		if $AVS_PLAYERTYPE['type']="QN":
			savegame '1.sav'
		else
			savegame 'save/autosave.sav'
			if $GAME_VALUE['save.reserve']<>'false': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
	else
		$print[]=$func('base.txt','nosave')
	end
end
if args[0]<>0 and args[0]<>999:
	time['goto']=1
	gosub 'true.goto'
end
--- avs.save ---------------------------------
