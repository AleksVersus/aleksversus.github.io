QSP-Game локация отвечает за добавление предмета в рюкзак
# add.obj
$args[9]=$args[9]	& !@ инициализация аргументов
!@ получаем позицию предмета в общей таблице объектов
args['pit']=arrpos('$id_array',$args[0])	& !@ получаем номер объекта в таблице объектов
$args['pos']=$func('get.tag.cont',$object_array[args['pit']],'pos')	& !@ видовой идентификатор возможного местоположения
$args['np']=$func('get.tag.cont',$object_array[args['pit']],"np")	& !@ набор признаков
$args['sw']=$func('get.tag.cont',$object_array[args['pit']])	& !@ идентификатор вида
if instr($args['np'],'[монета]')<>0:
	!@ если одним из признаков предмета является то, что он монета — деньги, ищем кошель
	$args['кошел']=$func('prv.obj.inBag','кошель')
	if $args['кошел']="":
	!@ если кошеля нет, создаём, для этого:
		!@ ищем место под кошель. Должно быть создано в самом начале игры!!!
		$args['пояс']=$func('prv.bag','','','пояс_кошель')
		!@ если место не найдено - создаем
		$args['кошел']=$func('crt.obj','кошель','','[quest]',0,1,$args['пояс'])
	end
	!@ получаем дочерние предметы кошеля
	killvar '$temp_id'	& !@ удаляем временный массив на всякий случай
	gosub 'get.daughter.obj',$args['кошел'],'\[:'+$args['sw']+':\]'	& !@ получаем идентификаторы предметов во временный массив
	$print[]=$func('base.txt','get',$args[0])	& !@ добавляем текст к выводимому
	if arrsize('$temp_id')>0:
	!@ если есть такой предмет, увеличиваем количество
		kolvo_array[arrpos('$id_array',$temp_id[0])]+=kolvo_array[args['pit']]
		gosub 'del.obj.id',$args[0]	& !@ удаляем предмет из старого расположения
	else
	!@ если нет такого предмета, добавляем
		$position_array[args['pit']]=$args['кошел']	& !@ подменяем расположение предмета на кошель
	end
	killvar '$temp_id'	& !@ удаляем временный массив на всякий случай
	jump 'ext'	& !@ все остальные скрипты пропускаем
end
!@ возможность подменить идентификатор (не помню, зачем это нужно)
if instr($run_array[args['pit']],'!addobj.id!')<>0: $args[0]=$func('run.dynamic.script','!addobj.id!',$args[0],'',$args[1])
!@ возможность подменить местоположение (вот это нужно, чтобы на стартовой странице предложить выбор между тремя предметами)
if instr($run_array[args['pit']],'!addobj.pos!')<>0: $args[1]=$func('run.dynamic.script','!addobj.pos!',$args[0],'',$args[1])
if $args[1] = '':
!@ если не указано место, в которое нужно добавить предмет, необходимо такое место найти. Это первая пустая ячейка рюкзака
	$args[1] = $func('prv.bag','','',$args['pos'])	& !@ ищем пустое место в рюкзаке (например, место для фляжки на поясе)
	if $GAME_VALUE['endlessbag']='true' and $args[1]='':
	!@ предназначено для отладчика! Если включен режим бесконечного рюкзака, создаём новую пустую ячейку
		$args[1] = $func('crt.obj','место_в_рюкзаке','','<body>',0,1,'INVENTORY')
	end
	!@ если нет пустой ячейки, значит предмет поднять нельзя, добавляем надпись, что предмет поднять нельзя и пропускаем остальное
	if $args[1] = '': $print[] = $func('base.txt','f',$args['pos']) & jump 'ext'
end
!@ определяем не присутствует ли в данной позиции предмет
args['posobjinpos']=arrpos('$position_array',$args[1])
if args['posobjinpos']<>-1:
!@ если место, в которое нам нужно положить предмет занято, выбрасываем предмет на текущей локации
	gosub 'put.obj',$id_array[args['posobjinpos']],'//nope.goto//'	& !@ сбрасываем предмет без автоперехода на локацию
end
$position_array[arrpos('$id_array',$args[0])]=$args[1] & !@ кладём предмет в указанное место
$print[]=$func('base.txt','add',$args[0])	& !@ добавляем текст подъёма предмета к выводу
!@ конец процедуры
:ext
if $args[2]<>'//nope.goto//':
!@ если не запрещён повторный переход на локацию
	!@ подмена перехода
	time['goto']=1
	!@ подмена перехода происходит, если на локации необходимо как-то альтернативным образом обработать подъём предмета
	if instr($run_array[args['pit']],'!addobj.goto!')<>0: $args['goto']=$func('run.dynamic.script','!addobj.goto!',$args[0],'',$args[1])
	if $args['goto']="":
		gosub 'true.goto'
	else
		goto $args['goto']
	end
end
--- add.obj ---------------------------------
АРГУМЕНТЫ:
$args[0] = $args[0]	&	!	идентификатор предмета
$args[1] = $args[1]	&	!	место в рюкзаке, куда хотим добавить предмет (идентификатор)
$args[2] = $args[2]	&	!	управляющая конструкция
