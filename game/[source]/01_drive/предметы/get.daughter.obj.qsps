QSP-Game ! Функция получает все дочерние объекты для данного ай-ди.
! Результатом является true - есть дочерние, false - нет дочерних
! так же заполняется массив $temp_id
! $args[0] - ай-ди родительского объекта
! $args[1] - регулярное выражение по которому производится дополнительная фильтрация
! $args[2] - массив, в который вносятся 
! $args[3] - дополнительная проверка. ! регулярное выражение по которому производится дополнительная фильтрация. Сортировка идёт по данным в массиве $run_array[]ай-ди дочерних объектов.
# get.daughter.obj
$args[9] = $args[9]
!@ восстанавливаем название массива для вывода результатов, если не указано, или указано не правильно
if $args[2] = '': $args[2] = '$temp_id'	
if instr($args[2],'$')<>1: $args[2] = '$'+$args[2]
$args['gdo']='false'
args['pos']=-1
:for
if $AVS_PLAYERTYPE['func_support'] = '5.8.0':
	args['pos']=arrpos('$position_array',$args[0],args['pos']+1)
else:
	args['pos']=arrpos(args['pos']+1,'$position_array',$args[0])
end
if args['pos']<>-1 and args['i']<>args['pos']:
	args['i']=args['pos']
	if $args[1]='' or $strfind($object_array[args['pos']],$args[1])<>'':
		if $args[3]='' or $strfind($run_array[args['pos']],$args[3])<>'':
			$args['gdo']='true'
			dynamic "<<$args[2]>>[arrsize('<<$args[2]>>')]=$id_array[args[0]]",args['pos']
		end
	end
	jump 'for'
end
$result=$args['gdo']
--- get.daughter.obj ---------------------------------
! ВНИМАНИЕ. Первый объект, созданный в игре,
! должен находиться вне области поиска дочерних объектов.
! Т.е. нельзя создавать первым объект, помещённый в другой объект.
