QSP-Game ! локация, выполняющая стандартную атаку
GAME_WAR['hero.fast'] - штраф или бонус к силе атаки
# int.war.attack
$args[0] = $args[0]	&	!@ идентификатор выбранного противника
$GAME_WAR['select.enemy']=$args[0]	& !@ отмечаем выбранного противника
$GAME_WAR_LOG[]=$func('base.txt','war.round')	& !@ добавляем в лог номер раунда
!@ сначала определяем собственные параметры
!@ получаем мощность удара из текущей ловкости и случайного числа
args['cube.hero']=rand(2,12)	& !@ выброшенные на кубиках очки
args['might.hero']=fast['now']+args['cube.hero']+GAME_WAR['hero.fast']	& !@ мощность удара героя (сила атаки героя)
!@ получаем список противников:
killvar '$temp_id'
gosub 'get.daughter.obj',$curloc,'<ratch>','$temp_id'
!@ ----------------------------------------- циклом перебираем противников ----------------------------------------------------
:for
!@ список параметров для получения силы атаки:
$args['attacks.all']="[pfast:<<fast['now']>>] [pcube:<<args['cube.hero']>>] [pfbns:<<GAME_WAR['hero.fast']>>]"
if arrsize('$temp_id')>0:
!@ цикл выполняется, пока не переберём всех противников
	if GAME_WAR['ratch.power.'+$temp_id[0]]>0:
	!@ если противник не исчерпал здоровье, с ним можно что-то сделать
		args['cube.enemy']=rand(2,12)	& !@ выброшенные противником на кубиках очки
		args['might.enemy']=GAME_WAR['ratch.fast.'+$temp_id[0]]+args['cube.enemy']	& !@ сила атаки противника
		args['pit']=arrpos('$id_array',$temp_id[0])	& !@ расположение противника в таблице объектов (т.о.)
		!@ список параметров для получения силы атаки противника
		$args['attacks.all']+="[emight:<<args['might.enemy']>>] [ecube:<<args['cube.enemy']>>] [eblow:<<GAME_WAR['ratch.blow.'+$temp_id[0]]>>]"
		!@ сравниваем силу атаки
		if args['might.enemy']>args['might.hero']:
		!@ если сила удара противника больше силы удара героя, наносим урон герою
			gosub 'power',-GAME_WAR['ratch.blow.'+$temp_id[0]],'war'	& !@ уменьшаем здоровье герою
			$GAME_WAR_LOG[]=$func('war.txt','eblow',$temp_id[0],$args['attacks.all'])	& !@ добавляем в лог надпись о нанесении урона
			!@ если есть какой-то навешанный на боёвку скрипт, запускаем
			if instr($GAME_WAR['DIN.'+$curloc],'!war.powerdown!')<>0:
				gosub 'run.dynamic.script','!war.powerdown!','//run.this.dynamic//',$GAME_WAR['DIN.'+$curloc]
			end
			!@ если здоровье героя упало ниже 1, игрок проиграл, добавляем надпись в лог
			if power['now']<1:
				$GAME_WAR_LOG[]=$func('war.txt','pdeath',$temp_id[0],$args['attacks.all'])
			end
		elseif args['might.enemy']<args['might.hero'] and $temp_id[0]=$args[0]:
		!@ если сила удара героя больше силы противника, и это противник, выбранный героем для атаки
			GAME_WAR['ratch.power.'+$temp_id[0]]-=2	& !@ уменьшаем здоровье противника
			!@ добавляем в список параметров текущее здоровье противника:
			$args['attacks.all']+=" [epower:<<GAME_WAR['ratch.power.'+$temp_id[0]]>>]"
			$GAME_WAR_LOG[]=$func('war.txt','pblow',$temp_id[0],$args['attacks.all'])	& !@ вносим в лог текст
			if GAME_WAR['ratch.power.'+$temp_id[0]]<1:
			!@ если здоровье противника упало ниже единицы
				$GAME_WAR_LOG[]=$func('war.txt','edeath',$temp_id[0],$args['attacks.all'])	& !@ вносим в лог текст
				!@ отмечаем, что противник мёртв соответствующим текстом
				$GAME_WAR['ratch.dead.'+$temp_id[0]]=$func('base.txt','ed',$func('get.tag.cont',$object_array[args['pit']],'sex'))
				GAME_WAR['death.enemy']+=1	& !@ число убитых противников увеличивается
				$GAME_WAR['select.enemy']=''	& !@ снимаем выделение с противника
			end
			!@ если есть какой-то навешанный на боёвку скрипт, запускаем:
			if instr($GAME_WAR['DIN.'+$curloc],'!war.ratchdown!')<>0:
				gosub 'run.dynamic.script','!war.ratchdown!','//run.this.dynamic//',$GAME_WAR['DIN.'+$curloc]
			end
		elseif args['might.enemy']<=args['might.hero'] and $temp_id[0]<>$args[0]:
		!@ если сила удара героя не меньше силы противника, и противник не выделен
			$GAME_WAR_LOG[]=$func('war.txt','pblock',$temp_id[0],$args['attacks.all'])	& !@ вносим текст в лог
		else
		!@ силы ударов совпадают, вносим соответствующий текст
			$GAME_WAR_LOG[]=$func('war.txt','block',$temp_id[0],$args['attacks.all'])
		end
	end
	killvar '$temp_id',0	& !@ удаляем айди рассматриваемого противника из списка
	jump 'for'	& !@ повторяем
end
killvar '$temp_id'	& !@ на всякий случай удаляем массив
GAME_WAR['round']+=1	& !@ увеличиваем число раундов
gosub 'int.war.prove'	& !@ проверяем победу, либо поражение
gosub 'int.war'	& !@ перезапускаем интерпретатор боя
--- int.war.attack ---------------------------------
