QSP-Game 
! локация организует меню для сохраннения и загрузки состояний игры
! Меню генерится и обрабатывается только этой локацией. Реализовано за счёт того, что локации
! обрабатывающей пункт меню, передаётся аргументом номер выбранного пункта. Годится только для
! меню фиксированного размера.
# avs.save
$args['save.name']=$func('#zero#',4-len($str(save['count']+1)))+$str(save['count']+1)+'_'
$args['loc']=$strfind($curloc,'\d+')
$args['save.name']+=$func('#zero#',3-len($args['loc']))+$args['loc']
if args[0]=0:
	!@ генерим меню
	killvar '$temp_menu'
	$temp_menu[]="Сохранить:avs.save"
	$temp_menu[]="Загрузить:avs.save"
	$temp_menu[]='-:-'
	if $AVS_PLAYERTYPE['type']='qSpider':
		$temp_menu[]="Загрузить чекпоинт [Q+W]:avs.save"
	else
		$temp_menu[]="Загрузить чекпоинт:avs.save"
	end
	$temp_menu[]="-:-"
	if $AVS_PLAYERTYPE['type']='qSpider':
		$temp_menu[]="Быстрое сохранение [Q+S]:avs.save"
	else
		$temp_menu[]="Быстрое сохранение:avs.save"
	end
	if $AVS_PLAYERTYPE['type']='qSpider':
		$temp_menu[]="Быстрая загрузка [Q+A]:avs.save"
	else
		$temp_menu[]="Быстрая загрузка:avs.save"
	end
	!@ вызываем меню
	menu '$temp_menu'
	!@ уничтожаем меню
	killvar '$temp_menu'
elseif args[0]=1:
	!@ сохранить состояние игры
	if nosave=0:
		save['count']+=1
		savegame
		if $GAME_VALUE['save.reserve']<>'false': 
			if $AVS_PLAYERTYPE='': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
	else
		$print[]=$func('base.txt','nosave')
	end
elseif args[0]=2:
	!@ открыть сохранённое состояние
	opengame
elseif args[0]=4:
	!@ загрузить чекпоинт
	if save['check']>0:
		if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']='qSpider':
			opengame '1.sav'
		else
			opengame 'save/autosave.sav'
		end
	else
		$print[]=$func('base.txt','notchp')
	end
elseif args[0]=6:
	!@ быстрое сохранение
	if nosave=0:
		save['qs']+=1
		save['count']+=1
		$save['type']='quick'
		if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']='qSpider':
			savegame '2.sav'
		else
			savegame 'save/quicksave.sav'
			if $GAME_VALUE['save.reserve']<>'false': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
		$print[]=$func('base.txt','save')
	else
		$print[]=$func('base.txt','nosave')
	end
elseif args[0]=7:
	!@ быстрая загрузка
	if save['qs']>0:
		if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']='qSpider':
			opengame '2.sav'
		else
			opengame 'save/quicksave.sav'
		end
	else
		$print[]=$func('base.txt','notqck')
	end
elseif args[0]=999:
	!@ автосохранение
	if nosave=0:
		save['check']+=1
		save['count']+=1
		$save['type']='auto'
		if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']='qSpider':
			savegame '1.sav'
		else
			savegame 'save/autosave.sav'
			if $GAME_VALUE['save.reserve']<>'false': savegame 'save/'+$save['game.number']+'_'+$args['save.name']+'.sav'
		end
	else
		$print[]=$func('base.txt','nosave')
	end
end
if args[0]<>0 and args[0]<>999:
	time['goto']=1
	gosub 'true.goto'
end
--- avs.save ---------------------------------
