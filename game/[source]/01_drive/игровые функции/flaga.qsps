# flaga
! наполняет флягу жидкостью
$args[0] = $args[0]	&	!	идентификатор фляги
if $args[0]='': $args[0]=$func('prv.obj.inBag','фляга')
if $args[1]='': $args[1]='вода'	&	!	идентификатор вида жидкости
if $args[0]!'':
	args['pit']=arrpos('$id_array',$args[0])		&	!	позиция фляги в таблице данных
	args['pot']=arrpos('$position_array',$args[0])	&	!	позиция текущей жидкости в таблице данных
	if instr($object_array[arrpos('$id_array',$position_array[args['pit']])],'[:пояс-фляга:]')!0: $args['outBag']='true'
	if args['pot']!-1:
		$args['name'] = $func('get.n.p',$object_array[args['pot']],'В')
		$print[]='<font color=#884400>Вы вылили <<$args["name"]>> из фляги.</font>'
		gosub 'del.obj.id',$id_array[args['pot']]	&	!	если во фляге есть какая-то жидкость, выливаем её.
	end
	$args['pot']=$func('crt.obj',$args[1],'','',0,1,$args[0])	&	!	создаём жидкость, которой хотим наполнить флягу
	charge_array[args['pit']]=func('get.tag.num',$object_array[args['pit']],'maxchrg')	&	!	заполняем флягу
	$args['name'] = $func('get.n.p',$object_array[arrpos('$id_array',$args['pot'])],'Т')
	if $args['outBag']='true':
		$print[]='<font color=#008888>Вы наполнили флягу <<$args["name"]>>.</font>'
	end
else
	$print[]='<font color=#880000>У вас нет фляги.</font>'
end
if $args[2]!'//nope.goto//':
	time['goto']=1
	gosub 'true.goto'
end
--- flaga ---------------------------------
