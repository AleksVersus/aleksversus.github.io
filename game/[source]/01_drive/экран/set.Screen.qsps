QSP-Game локация предназначена для переключения режимов настройки интерфейса (экрана)
'обложка.чёрный' — это режим начальной обложки, то есть окна лаунчера и стартового экрана
'страница.сброс' — режим всех страниц игры
# set.Screen
!@ мы полностью отказываемся от всех окон, кроме основного описания:
showacts 0	&	!@ скрываем действия
showobjs 0	&	!@ скрываем вещи
showstat 0	&	!@ скрываем дополнительное описание
if $args[0] = 'обложка.чёрный':
	!@ для классики и qSpider можно определить настройки с помощью данных переменных:
	fcolor = rgb (0,200,0)		&	!@	настраиваем текущий цвет шрифта - зелёный
	bcolor = rgb (15,16,17)		&	!@	настраиваем текущий цвет фона - чёрный
	lcolor = rgb (0,100,0)		&	!@	настраиваем текущий цвет ссылок - тёмно-зелёный
	$fname = 'Comic Sans MS'	&	!@	устанавливаем шрифт
	!@ настройки для навигатора:
	$args["bc"]="000000"	& !@ цвет фона для навигатора
	$args["fc"]="00C800"	& !@ цвет шрифта для навигатора
	$args["lc"]="006400"	& !@ цвет ссылок (в навигаторе не работает)
elseif $args[0] = 'страница.сброс':
	!@ 
	$args["bc"]="ffffff"	& !@ цвет фона для навигатора
	$args["fc"]="000000"	& !@ цвет шрифта для навигатора
	$args["lc"]="008888"	& !@ цвет ссылок (в навигаторе не работает)
	$fname = 'Consolas'		&	!@	устанавливаем шрифт
	!@ настройки для навигатора:
	fcolor = rgb(0,0,0)			&	!@	настраиваем текущий цвет шрифта по умолчанию
	bcolor = rgb(255,255,255)	&	!@	настраиваем текущий цвет фона - по умолчанию
	lcolor = rgb(0,136,136)		&	!@	настраиваем текущий цвет ссылок
end
!@ задаём разные настройки для квест навигатора, если плеер определён, как навигатор
if instr($AVS_PLAYER,'[QN]')<>0:
	EXEC('JS:document.body.style.backgroundColor="#'+$args["bc"]+'";')
	EXEC('JS:document.body.style.color="#<<$args["fc"]>>";')
end
!@ пока не удалось заставить навигатор перерисовывать цвет гиперссылок.
!@ дело в том, что свойство linkColor не срабатывает после загрузки страницы
!@ EXEC('JS:document.linkColor="#<<$args["fc"]>>";')
$GAME_INTERFACE['refresh']=''
--- set.Screen ---------------------------------
