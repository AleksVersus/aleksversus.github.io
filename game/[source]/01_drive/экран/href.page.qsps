# href.page
! генерирует из строки ссылку на страницу
$args[0] = $args[0]	&	! строка
if instr($args[0],'[page=')=1:
	$args['page']=$strfind($args[0],'\[page=(\d+)\]',1)	&	!	получаем номер страницы
	$args['href']=$replace($replace($args[0],'[/page]'),'[page='+$args['page']+']')	&	!	получаем текст ссылки
elseif instr($args[0],'<a l:href="#n_')=1:
	$args['page']=$strfind($args[0],'<a l:href="#n_(\d+)">',1)	&	!	получаем номер страницы
	$args['href']=$replace($replace($args[0],'</a>'),'<a l:href="#n_'+$args['page']+'">')	&	!	получаем текст ссылки
end
if instr($lvar['заголовок'],'<avs-death>')=0:
	$args['res']='<a href="exec:time[''goto'']=1 & goto ''page.'+$args['page']+'''" title="переход на другую страницу" class="plain">'
	$args['res']+=$args['href']+'</a>'	&	!	формируем ссылку
else
	$args['res']=$args['href']
end
$result=$args['res']
--- href.page ---------------------------------
