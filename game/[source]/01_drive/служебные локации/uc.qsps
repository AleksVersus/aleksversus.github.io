QSP-Game ! микро-отладчик
! "команды
	p0 - переход на страницу 0. Вместо 0 можно указать номер любой другой страницы
	log - вывод логов
	objs - вывод таблицы всех объектов игры
	имя_переменной/a - вывод содержимого массива с указанным именем (ключ записывается с латинской a)
	way - вывод всех локаций, по которым продвигался игрок
	eb - включение и выключение бесконечного рюкзака
!"
# uc
if strcomp($user_text,'p\d+')<>0:
	$GAME_VALUE['interface']='[game]'
	$args['loc']='page.'+$strfind($user_text,'\d+')
	if (loc($args['loc']))<>0:
	!@ если локация существует, переходим
		goto $args['loc']
	else
	!@ если локация не существует, выводим текст на экран
		*pl "Такая страница не существует"
	end
elseif $user_text='objs':
	*pl $func('uc.obj')
elseif $user_text='eb' or $user_text='endless bug':
	if $GAME_VALUE['endlessbag']<>'true':
		$GAME_VALUE['endlessbag']='true'
		*pl "Включён бесконечный рюкзак."
	else
		$GAME_VALUE['endlessbag']=''
		*pl "Бесконечный рюкзак выключен."
	end
elseif $user_text='log':
	*pl $func('#array.prnt#','$log_error')
elseif $user_text='way':
	*pl $func('#array.prnt#','$survived_location')
elseif instr($user_text,'/a')=len($user_text)-1:
	*pl $func('#array.prnt#',$trim($mid($user_text,1,len($user_text)-2)))
elseif strcomp($user_text,'o\d+')<>0:
	args[9]=$strfind($user_text,'\d+')
	$replace($replace("
	$id_array[<<args[9]>>]='<<$id_array[args[9]]>>'
	$object_array[<<args[9]>>]='<<$object_array[args[9]]>>'
	$position_array[<<args[9]>>]='<<$position_array[args[9]]>>'
	kolvo_array[<<args[9]>>]=<<kolvo_array[args[9]]>>
	charge_array[<<args[9]>>]=<<charge_array[args[9]]>>
	$run_array[<<args[9]>>]='<<$run_array[args[9]]>>'",'<','&lt;'),'>','&gt;')
else
	dynamic $user_text
end
--- uc ---------------------------------
