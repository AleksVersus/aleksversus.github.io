QSP-Game самая первая локация. Здесь может быть заставка или вступительное слово
# [start]
gosub 'set.newgame','kill'	& !@ выставляем настройки по-умолчанию. !!! НУЖНО ЛИ???
$GAME_VALUE['interface']=''	& !@ установки интерфейса. Пустое значение: текст игры без доп элементов интерфейса.
$save['game.number']=$func('#rndstr#',4,'буквы и цифры') & !@ генерим уникальный номер игры
!@ разделяем типы, чтобы не заморачиваться с функциями instr
if instr($AVS_PLAYERTYPE,"[MINI]")<>0: $AVS_PLAYERTYPE['size']="MINI" else $AVS_PLAYERTYPE['size']=""
if instr($AVS_PLAYERTYPE,"[QN]")<>0:
	$AVS_PLAYERTYPE['type']="QN"	& !@ Quest Navigator
elseif instr($AVS_PLAYERTYPE,"[qSpider]")<>0:
	$AVS_PLAYERTYPE['type']="qSpider"	& !@ qSpider и классический версии 5.8.0
else
	$AVS_PLAYERTYPE['type']=""	& !@ классический 5.7.0
end
gosub 'set.Screen','обложка.чёрный'	& !@ настраиваем экран

!@ Набираем текст для последующего вывода на экран:
$args['author']='ОЛЬГА ГОЛОТВИНА'
$args['head.floor1']='ВЕРЕНИЦА МИРОВ'
$args['subhead.floor1']='или'
$args['head.floor2']='ВЫВОДЫ ИЗ ЗАКОНА МЕРФИ'
$args['subhead.floor2']='Книга-игра'
$args['epigraph.1']='Если неприятность может произойти, она обязательно происходит.'
$args['epigraph.2']='Закон Мерфи, 2-я пол. ХХ века'
$args['link.startgame']='<a href="exec:goto ''page.-1''" id="start_the_game" class="plain">Начать игру</a>'
$args['link.gameplay']='<a href="exec:goto ''[1.1_gameplay]''" id="about_gameplay" class="plain">Об игре</a>'
$args['link.release']='<a href="exec:goto ''[1.0_about]''" id="about_release" class="plain">О текущей версии</a>'
!@ для каждого плеера собственные настройки оформления. Зачем добиваться одинакового отображения, если все плееры разные?
if $AVS_PLAYERTYPE['size']<>"MINI":
	!@ только для десктопных версий плееров пишем собственные схемы вывода
	if $AVS_PLAYERTYPE['type']="QN" or $AVS_PLAYERTYPE['type']="qSpider":
		!@ для Quest Navigator и qSpider
		$args['screen']+='<div class="main_cover">'
			$args['screen']+='<span class="author">'+$args['author']+'</span><br>'
			$args['screen']+='<span class="heading">'+$args['head.floor1']+'</span><br>'
			$args['screen']+='<span class="subhead" style="color:#dddd00;">'+$args['subhead.floor1']+'</span><br>'
			$args['screen']+='<span class="heading">'+$args['head.floor2']+'</span><br>'
			$args['screen']+='<span class="subhead">'+$args['subhead.floor2']+'</span>'
			$args['screen']+='<br><br><div class="epigraph">'
				$args['screen']+='<div id="epigraph_1">'+$args['epigraph.1']+'</div>'
				$args['screen']+='<div id="epigraph_2">'+$args['epigraph.2']+'</div>'
			$args['screen']+='</div><br><br>'
			$args['screen']+=$args['link.startgame']+'<br><br>'
			$args['screen']+=$args['link.gameplay']+'<br><br>'
			$args['screen']+=$args['link.release']+''
		$args['screen']+='</div>'
	else
		!@ для классического плеера
		
	end
end
!@ выводим обложку:
*p $args['screen']
showinput 1	& !@ включаем строку ввода для отладки
! ВСЕМУ СООБЩЕСТВУ QSP, людям из quest-book, и просто проходившим мимо СПАСИБО !
--- [start] ---------------------------------
